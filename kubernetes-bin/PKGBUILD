# Maintainer: Vianney Bouchaud <aur dot vianney at bouchaud dot org>
# https://github.com/kubernetes/kubernetes/blob/master/CHANGELOG/CHANGELOG-1.29.md#server-binaries

pkgbase=kubernetes-bin

pkgname=(
    kubeadm-bin
    kubelet-bin
)

pkgdesc='Kubernetes.io server binary'
pkgver=1.29.4
pkgrel=1
arch=('x86_64' 'aarch64')
url='https://kubernetes.io'
license=('apache')
conflicts=(
    'kubernetes'
)
provides=()

source_x86_64=(
    "kubernetes-${pkgver}.tgz::https://dl.k8s.io/v${pkgver}/kubernetes-node-linux-amd64.tar.gz"
)
source_aarch64=(
    "kubernetes-${pkgver}.tgz::https://dl.k8s.io/v${pkgver}/kubernetes-node-linux-arm64.tar.gz"
)

sha512sums_x86_64=(
    'bf5eb2f4ef8e215941d98ca62bc4901fcad7f68dda153e1f077f9688b1e3f273d3b25bc48167cefd0a1f1ce1e0a3525d71bf7aa37b7fa8b8f639af35df0233bd'
)
sha512sums_aarch64=(
    'd983706b1975d3b8c3cb1dae833efc178337df2c2abdd835588a0be6dbaef55c27e1df993e8623b87f56f7f7a8f6de34390e8a085911644fa8af2d49f47512e0'
)

package_kubeadm-bin() {
    pkgdesc='Kubernetes.io kubeadm binary'
    conflicts+=(
        kubeadm
    )
    provides+=(
        kubeadm
    )
    depends=(
        kubelet
    )

    install -D -m0755 "kubernetes/server/bin/kubeadm" "${pkgdir}/usr/bin/kubeadm"
    "${pkgdir}/usr/bin/kubeadm" completion bash | install -D -m644 /dev/stdin "${pkgdir}/usr/share/bash-completion/completions/kubeadm"
    "${pkgdir}/usr/bin/kubeadm" completion zsh | install -D -m644 /dev/stdin "${pkgdir}/usr/share/zsh/site-functions/_kubeadm"

    cat <<EOF | install -D -m644 /dev/stdin "${pkgdir}/etc/systemd/system/kubelet.service.d/10-kubeadm.conf"
cat etc/systemd/system/kubelet.service.d/10-kubeadm.conf
# Note: This dropin only works with kubeadm and kubelet v1.11+
[Service]
Environment="KUBELET_KUBECONFIG_ARGS=--bootstrap-kubeconfig=/etc/kubernetes/bootstrap-kubelet.conf --kubeconfig=/etc/kubernetes/kubelet.conf"
Environment="KUBELET_CONFIG_ARGS=--config=/var/lib/kubelet/config.yaml"
# This is a file that "kubeadm init" and "kubeadm join" generates at runtime, populating the KUBELET_KUBEADM_ARGS variable dynamically
EnvironmentFile=-/var/lib/kubelet/kubeadm-flags.env
# This is a file that the user can use for overrides of the kubelet args as a last resort. Preferably, the user should use
# the .NodeRegistration.KubeletExtraArgs object in the configuration files instead. KUBELET_EXTRA_ARGS should be sourced from this file.
EnvironmentFile=-/etc/default/kubelet
ExecStart=
ExecStart=/usr/bin/kubelet \$KUBELET_KUBECONFIG_ARGS \$KUBELET_CONFIG_ARGS \$KUBELET_KUBEADM_ARGS \$KUBELET_EXTRA_ARGS
EOF
}

package_kubelet-bin() {
    pkgdesc='Kubernetes.io kubelet binary'
    conflicts+=(
        'kubelet'
    )
    provides+=(
        'kubelet'
    )
    depends=(
        crictl
    )

    install -D -m0755 "kubernetes/server/bin/kubelet" "${pkgdir}/usr/bin/kubelet"

    cat <<EOF | install -D -m644 /dev/stdin "${pkgdir}/usr/lib/systemd/system/kubelet.service"
[Unit]
Description=kubelet: The Kubernetes Node Agent
Documentation=https://kubernetes.io/docs/home/
Wants=network-online.target
After=network-online.target

[Service]
ExecStart=/usr/bin/kubelet
Restart=always
StartLimitInterval=0
RestartSec=10

[Install]
WantedBy=multi-user.target
EOF
}
